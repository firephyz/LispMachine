<HTML>
<HEAD>
<!-- This HTML file has been created by texi2html 1.54
     from r5rs.texi on 8 March 1998 -->

<TITLE>Revised(5) Report on the Algorithmic Language Scheme - Macros for derived expression types</TITLE>
<link href="r5rs_80.html" rel=Next>
<link href="r5rs_78.html" rel=Previous>
<link href="r5rs_toc.html" rel=ToC>

</HEAD>
<BODY>
<p>Go to the <A HREF="r5rs_1.html">first</A>, <A HREF="r5rs_78.html">previous</A>, <A HREF="r5rs_80.html">next</A>, <A HREF="r5rs_83.html">last</A> section, <A HREF="r5rs_toc.html">table of contents</A>.
<P><HR><P>


<H2><A NAME="SEC81" HREF="r5rs_toc.html#TOC81">7.3  Derived expression types</A></H2>

<P>
This section gives macro definitions for the derived expression types in terms
of the primitive expression types (literal, variable, call, lambda, if, set!).
See section 6.4 for a possible definition of delay.

</P>

<PRE>
  (define-syntax cond
    (syntax-rules (else =&#62;)
      ((cond (else result1 result2 ...))
       (begin result1 result2 ...))
      ((cond (test =&#62; result))
       (let ((temp test))
         (if temp (result temp))))
      ((cond (test =&#62; result) clause1 clause2 ...)
       (let ((temp test))
         (if temp
             (result temp)
             (cond clause1 clause2 ...))))
      ((cond (test)) test)
      ((cond (test) clause1 clause2 ...)
       (let ((temp test))
         (if temp
             temp
             (cond clause1 clause2 ...))))
      ((cond (test result1 result2 ...))
       (if test (begin result1 result2 ...)))
      ((cond (test result1 result2 ...)
             clause1 clause2 ...)
       (if test
           (begin result1 result2 ...)
           (cond clause1 clause2 ...)))))

<PRE>

</PRE>

  (define-syntax case
    (syntax-rules (else)
      ((case (key ...)
         clauses ...)
       (let ((atom-key (key ...)))
         (case atom-key clauses ...)))
      ((case key
         (else result1 result2 ...))
       (begin result1 result2 ...))
      ((case key
         ((atoms ...) result1 result2 ...))
       (if (memv key '(atoms ...))
           (begin result1 result2 ...)))
      ((case key
         ((atoms ...) result1 result2 ...)
         clause clauses ...)
       (if (memv key '(atoms ...))
           (begin result1 result2 ...)
           (case key clause clauses ...)))))

<PRE>

</PRE>

  (define-syntax and
    (syntax-rules ()
      ((and) #t)
      ((and test) test)
      ((and test1 test2 ...)
       (if test1 (and test2 ...) #f))))

<PRE>

</PRE>

  (define-syntax or
    (syntax-rules ()
      ((or) #f)
      ((or test) test)
      ((or test1 test2 ...)
       (let ((x test1))
         (if x x (or test2 ...))))))

<PRE>

</PRE>

  (define-syntax let
    (syntax-rules ()
      ((let ((name val) ...) body1 body2 ...)
       ((lambda (name ...) body1 body2 ...)
        val ...))
      ((let tag ((name val) ...) body1 body2 ...)
       ((letrec ((tag (lambda (name ...)
                        body1 body2 ...)))
          tag)
        val ...))))

<PRE>

</PRE>

  (define-syntax let*
    (syntax-rules ()
      ((let* () body1 body2 ...)
       (let () body1 body2 ...))
      ((let* ((name1 val1) (name2 val2) ...)
         body1 body2 ...)
       (let ((name1 val1))
         (let* ((name2 val2) ...)
           body1 body2 ...)))))
</PRE>

<P>
The following letrec macro uses the symbol &#60;undefined&#62; in place of an
expression which returns something that when stored in a location makes it an
error to try to obtain the value stored in the location (no such expression is
defined in Scheme).  A trick is used to generate the temporary names needed to
avoid specifying the order in which the values are evaluated.  This could also
be accomplished by using an auxiliary macro.

</P>

<PRE>
  (define-syntax letrec
    (syntax-rules ()
      ((letrec ((var1 init1) ...) body ...)
       (letrec "generate_temp_names"
         (var1 ...)
         ()
         ((var1 init1) ...)
         body ...))
      ((letrec "generate_temp_names"
         ()
         (temp1 ...)
         ((var1 init1) ...)
         body ...)
       (let ((var1 &#60;undefined&#62;) ...)
         (let ((temp1 init1) ...)
           (set! var1 temp1)
           ...
           body ...)))
      ((letrec "generate_temp_names"
         (x y ...)
         (temp ...)
         ((var1 init1) ...)
         body ...)
       (letrec "generate_temp_names"
         (y ...)
         (newtemp temp ...)
         ((var1 init1) ...)
         body ...))))

<PRE>

</PRE>

  (define-syntax begin
    (syntax-rules ()
      ((begin exp ...)
       ((lambda () exp ...)))))
</PRE>

<P>
The following alternative expansion for begin does not make use of the ability
to write more than one expression in the body of a lambda expression.  In any
case, note that these rules apply only if the body of the begin contains no
definitions.

</P>

<PRE>
  (define-syntax begin
    (syntax-rules ()
      ((begin exp)
       exp)
      ((begin exp1 exp2 ...)
       (let ((x exp1))
         (begin exp2 ...)))))
</PRE>

<P>
The following definition of do uses a trick to expand the variable clauses.  As
with letrec above, an auxiliary macro would also work.  The expression (if #f
#f) is used to obtain an unspecific value.

</P>

<PRE>
  (define-syntax do
    (syntax-rules ()
      ((do ((var init step ...) ...)
           (test expr ...)
           command ...)
       (letrec
         ((loop
           (lambda (var ...)
             (if test
                 (begin
                   (if #f #f)
                   expr ...)
                 (begin
                   command
                   ...
                   (loop (do "step" var step ...)
                         ...))))))
         (loop init ...)))
      ((do "step" x)
       x)
      ((do "step" x y)
       y)))
</PRE>

<P><HR><P>
<p>Go to the <A HREF="r5rs_1.html">first</A>, <A HREF="r5rs_78.html">previous</A>, <A HREF="r5rs_80.html">next</A>, <A HREF="r5rs_83.html">last</A> section, <A HREF="r5rs_toc.html">table of contents</A>.
</BODY>
</HTML>
